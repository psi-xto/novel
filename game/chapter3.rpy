
image office_soc  = "images/bgs/2.1.png"
image workspace = "images/bgs/2.2.png"
image office_soc_alert  = "images/bgs/4.1.jpg"

define a = Character("Алексей", color="#c8ffc8")
define m = Character("Максим", color="#c8c8ff")

image logs_interface = "images/chapter3/sshd.png"
image dlp_logs = "images/chapter3/dlp.png"

transform screen_center:
    xalign 0.5
    yalign 0.5

label chapter_3:
    
    $ chapter3_choice1 = 0
    $ chapter3_choice2 = 0
    $ chapter3_choice3 = 0
    $ decision_made = False
    
    stop music fadeout 2.0
    scene black
    with fade
    
    show text "{b}Глава 3\nВНУТРЕННЯЯ УГРОЗА{/b}" with dissolve
    $ renpy.pause(2.0)
    hide text with dissolve
    
    scene office_soc
    with fade
    
    
    "В офисе царит обычная рабочая атмосфера, когда внезапно..."
    play music alarm volume 0.5 fadein 2.5

    "Резкий звук критического уведомления нарушает тишину."
    show office_soc_alert with dissolve
    show max_2 at Transform(xalign=1.0, ypos=0.04, zoom=1.15) with moveinright
    m "Алексей. Срочно. У нас инцидент уровня 'красный'."
    show alex_wow at Transform(xalign=0.03, ypos=0.1, zoom=1.08) with moveinleft
    a "Что случилось?"

    m "Сервер бухгалтерии в «Копеечке» — ведёт себя странно."
    m "Общая нагрузка на CPU — 95 процентов. Всё в 3:17 ночью."

    a "Это снова майнинг?"

    m "(кивает) Да. Но не снаружи."

    "Максим делает паузу, его лицо становится серьёзным."
    
    m "Системный администратор. Его имя — Андрей Соколов."
    
    a "...Он же тот, кто на прошлой неделе помог мне с настройкой логов."
    
    m "(тихо) Ты знаешь, что значит, когда человек, которому ты доверяешь, становится угрозой?"
    
    "Максим достаёт бумажку с распечаткой логов."
    
    m "Это не внешняя атака. Это — внутренняя."
    m "Твоя задача — найти доказательства."
    m "И... принять решение."
    
    menu:
        "Сначала проверю его учётную запись — кто и когда входил?":
            $ analytical_score += 1
            $ chapter3_choice1 = 1
            
            a "Сначала проверю его учётную запись — кто и когда входил?"
            m "Логичный подход. Начинай с аудита авторизаций."
            
        "Нужно срочно отключить сервер — пока не разнесло всю сеть!":
            $ impulsive_score += 2
            $ chapter3_choice1 = 2
            
            a "Нужно срочно отключить сервер!"
            m "Стоп. Если отключим без анализа, мы потеряем все доказательства."
            m "И создадим проблемы для бизнеса. Действуй обдуманно."
            
        "Почему он? Он же всегда вежливый, спокойный... Может, это ошибка системы?":
            $ practical_score += 1
            $ chapter3_choice1 = 3
            
            a "Он же всегда вежливый... Может, это ошибка системы?"
            m "Доверие — хорошо. Но в безопасности нужно проверять всё."
            m "Давай посмотрим факты, а потом сделаем выводы."
    
    ## СЦЕНА 3.2: АНАЛИЗ СЕРВЕРНЫХ ЛОГОВ
    scene workspace
    with fade
    
    #play music ch3_investigation volume 0.25
    
    "Вы подключаетесь к серверу клиента. Начинаете анализ логов."
    
    show logs_interface with dissolve:
        xalign 0.5
        yalign 0.4
    
    a "Проверяю логи удаленного подключения..."
    play sound sms volume 0.5

    "`Jul 12 03:17:03 server sshd[2890]: Принят пароль для andrey из 192.168.1.10`"
    
    a "И сразу после входа..."
    play sound sms volume 0.5

    "`Jul 12 03:17:05 server systemd[1]: Запущена служба cryptomining`"
            
    a "График нагрузки на CPU..."
    "На графике видна чёткая закономерность: каждую ночь с 3:17 до 5:45 — загрузка 95–99 процентов."
            
    a "Сетевой трафик..."
    "Исходящие соединения на IP `185.142.xx.xx` — известный майнинг-пул Monero."
        
    m "Что видишь? Сделай выводы."
    hide logs_interface with dissolve
    menu:
        "Это майнинг — типичная нагрузка и подключения к пулу":
            $ analytical_score += 2
            $ chapter3_choice2 = 1
            
            a "Это майнинг. Типичная нагрузка на CPU и подключения к известному пулу."
            m "Верно. Все признаки совпадают."
            
        "Может, это обновление 1С? Серверные процессы бывают тяжёлыми":
            $ impulsive_score += 1
            $ chapter3_choice2 = 2
            
            a "Может, это обновление 1С? Серверные процессы бывают тяжёлыми."
            m "Обновления не запускаются в 3:17 ночи каждый день."
            m "И не подключаются к китайским майнинг-пулам."
            
        "Нужно срочно отключить сервер!":
            $ impulsive_score += 2
            $ practical_score -= 1
            $ chapter3_choice2 = 3
            
            a "Нужно срочно отключить сервер!"
            m "Мы уже обсуждали это. Без полного анализа ты можешь уничтожить улики."
            m "Или упустить соучастников."
    
    scene office_soc
    with fade
    
    
    show max_2 at Transform(xalign=0.2, ypos=0.04, zoom=1.15) with dissolve
    show alex_nb at Transform(xalign=0.3, ypos=0.1, zoom=1.03) with dissolve
    
    m "Технические доказательства  есть. Теперь посмотрим на человека."
    scene workspace with dissolve
    show dlp_logs at Transform(xalign=0.5, yalign=0.4) with dissolve
    
    m "Посмотри на активность его учётной записи в июне."
    m "Пик финансовых операций: переводы, возвраты, проверки баланса."
    
    "Сообщения, зафиксированные вне корпоративного взаимодействия:"
    play sound sms volume 0.5
    "• Сообщение: «Андрей, обмен вашей базы с моей доплатой рассмотрите?»"
    play sound sms volume 0.5

    "• Запрос на смену пароля в нерабочее время"
    play sound sms volume 0.5

    "• Сообщение: «Андрей, когда вернешь долг? Скоро у тебя начнутся проблемы. »"
    play sound sms volume 0.5

    "• Активность в 22:28, 02:15, 03:17. Утечки."
        
    m "Он не злодей. Он — человек в нужде."
    m "А это — его уязвимость."
    hide dlp_logs with dissolve
    menu:
        "Он хотел подзаработать. Это не злой умысел — просто отчаяние.":
            $ analytical_score += 2
            $ chapter3_choice3 = 1
            
            a "Он хотел подзаработать. Это не злой умысел — просто отчаяние."
            m "Понимание мотива важно. Но нарушение остаётся нарушением."
            
        "Неважно почему. Он нарушил политику безопасности. Нужно увольнять.":
            $ impulsive_score += 2
            $ chapter3_choice3 = 2
            
            a "Неважно почему. Он нарушил политику безопасности."
            m "Холодно, но по букве закона — верно."
            m "Только помни: за каждым логом — живой человек."
            
        "Предложить ему пройти обучение и перевести в другую команду?":
            $ practical_score += 2
            $ chapter3_choice3 = 3
            
            a "Может, предложить ему обучение и перевод в другую команду?"
            m "Гуманистический подход. Но компания должна решить, готова ли она на риск."
    
    scene office_soc_alert
    with fade
    scene office_soc with dissolve
    
    stop music fadeout 3.0
    play music servers fadein 5.0 volume 0.5  
    "Вы с Максимом в серверной. Тишину нарушает только гул оборудования."
    
    show max_2 at Transform(xalign=0.5, ypos=0.04, zoom=1.15) with dissolve
    
    m "(стоя у стойки, не оборачиваясь)"
    m "Ты нашёл доказательства."
    m "Теперь — твоя очередь."
    m "Что делать с Андреем?"
    hide max_2    
    menu:
        "Собрать полный отчёт и передать СБ. Пусть решают.":
            $ analytical_score += 2
            $ practical_score += 1
            $ decision_made = True
            
            a "Соберу полный отчёт и передам в СБ. Пусть решают."
            m "Ты сделал всё по инструкции. Это правильно."
            m "Твоя работа — предоставить факты. Их работа — принимать кадровые решения."
            
        "Сразу заблокировать учётную запись и вызвать охрану.":
            $ impulsive_score += 3
            $ decision_made = True
            
            a "Сразу заблокировать УЗ и вызвать охрану."
            m "Ты спас компанию от дальнейшего ущерба."
            m "Но потерял человека, который, возможно, мог исправиться."
            m "В безопасности всегда есть этот выбор: система или человек?"
            
        "Позвонить ему. Спросить, почему. Дать шанс.":
            $ practical_score += 2
            $ analytical_score += 1
            $ decision_made = True
            
            a "Позвоню ему. Спрошу, почему. Дам шанс объясниться."
            m "Ты не просто аналитик. Ты — человек."
            m "Но помни: этот разговор может быть использован против тебя."
            m "И против компании."
    
    
    scene office_soc
    with fade
        
    show max_1 at Transform(xalign=1.0,ypos=0.04, zoom=1.15) with dissolve
    show alex_normal at Transform(left, ypos=0.04, zoom=1.2) with dissolve 
    
    m "Внутренние угрозы — самые сложные."
    m "Здесь нет чёрного и белого. Только оттенки серого."
    
    if analytical_score >= 6 and chapter3_choice3 == 1:
        a "Я понял главное: нужно видеть не только технологию, но и человека за ней."
        m "Это и делает тебя хорошим аналитиком."
        
    elif impulsive_score >= 6 and chapter3_choice3 == 2:
        a "Правила есть правила. Их нарушение должно иметь последствия."
        m "Строго, но справедливо. В некоторых компаниях это ценится."
        
    elif practical_score >= 6 and chapter3_choice3 == 3:
        a "Баланс между безопасностью и человечностью... это сложно."
        m "И этому не учат в учебниках. Только опыт."
    
    m "Завтра будет новый день."
    m "И новые решения."
    
    scene black with fade
    
    $ renpy.save("chapter3_complete", "Глава 3 завершена")
    
    jump epilogue
    