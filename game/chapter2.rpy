## ГЛАВА 2: ЦИФРОВОЙ ДЕТЕКТИВ

# Определение персонажей
image bg office_soc  = "images/bgs/2.1.png"

define a = Character("Алексей", color="#c8ffc8")
define m = Character("Максим", color="#c8c8ff")
define d = Character("Дмитрий", color="#ffc8c8")
transform screen_center:
    xalign 0.5
    yalign 0.5
# Изображения
image bg phantom_office = "images/bgs/office_phantom.png"
image downloads = "images/ui/downloads.png"
image virustotal = 'images/ui/virustotal.png'
image configxml = 'images/ui/config.png'
image smslog = 'images/ui/smslog.png'

# Основная метка главы 2
label chapter_2:
    
    # Инициализация переменных
    $ chapter2_choice1 = 0
    $ chapter2_choice2 = 0
    $ chapter2_choice3 = 0
    $ chapter2_choice4 = 0
    $ cards_collected = 0
    $ chain_complete = False
    
    stop music fadeout 2.0
    scene black
    with fade
    
    show text "Глава 2\nЦИФРОВОЙ ДЕТЕКТИВ" with dissolve
    $ renpy.pause(2.0)
    hide text with dissolve
        
    ## СЦЕНА 2.1: ПРОСЬБА О ПОМОЩИ
    scene bg office_soc 
    with fade
    
    play sound door
    play music ch2fon volume 0.1
    
    "Дверь открывается с лёгким скрипом. В офис заходит мужчина лет сорока, нервно оглядываясь."
    
    show dmitriy at Transform(left, ypos=0.04, zoom=0.68) with dissolve
    
    d "Мне сказали, вы помогаете с... с телефонами."
    d "У меня пропали деньги. С карты. Дважды. И SMS исчезают."
    
    show max_1 at Transform(xalign=1.0,ypos=0.04, zoom=1.15) with moveinright
    
    m "Садитесь, Дмитрий Иванович. Мы разберёмся."
    m "Алексей, займись снятием дампа с его телефона."

    hide max_1 with fade

    show alex_nb at Transform(xalign=0.95,ypos=0.1, zoom=1.03) with moveinright

    
    d "Если что — я не хакер, я просто... хотел продать запчасти..."
    
    menu:
        "Не переживайте, мы всё восстановим. Расскажите, что делали в последние дни?":
            $ analytical_score += 1
            
            a "Не переживайте, мы всё восстановим. Расскажите, что делали в последние дни?"
            d "Ну... работаю в логистике. Денег вечно не хватает... решил старые запчасти продать."
            d "Скачал Авито, там и выставил. Потом какой-то покупатель написал..."
            
        "Главное — не трогайте телефон дальше. Давайте дамп и я начну анализ.":
            $ practical_score += 1
            
            a "Главное — не трогайте телефон дальше. Давайте дамп и я начну анализ."
            d "Ох... да, конечно. Вот..."
            "Дмитрий протягивает телефон, его руки слегка дрожат."
            
        "Опять фишинг... Сколько можно! Ладно, щас гляну, что там у вас за вирус.":
            $ impulsive_score += 1
            
            a "Опять фишинг... Ладно, щас гляну, что там у вас за вирус."
            m "Алексей..."
            "Дмитрий выглядит обиженным. Он меньше делится подробностями."
    
    ## СЦЕНА 2.2: УЛИКА №1 — ПОДОЗРИТЕЛЬНЫЙ APK
    
    scene bg workspace
    with fade
    
    "Вы подключаете телефон к рабочей станции. Начинается анализ дампа - снимка всех данных с телефона."
    a "Говорите, вы Авито скачали. Проверим загрузки..."
    show downloads at screen_center with dissolve
    $ renpy.pause(1.0)
    a "В папке загрузок... странный файл."
    play music keyboard fadein 5 fadeout 5
    "`Авито Фото.apk` — размер 4.7 МБ, дата установки: вчера, 23:47."
    hide downloads
    # Флешбэк №1
    # play music flashback_music fadein 1.0
    # show flashback_garage with flash
    # flashback "Гараж. Поздний вечер. Дмитрий возится с двигателем."
    # play sound phone_notification
    # flashback "На телефон приходит сообщение: «Скачайте Авито Фото — там все фото видно. Без этого приложения фотографии запчастей не открываются»."
    # flashback "Уставший, он кликает на ссылку..."
    # hide flashback_garage with dissolve
    # stop music fadeout 1.0
    # play music tense_music fadein 1.0
    
    menu:
        "Проверить файл в VirusTotal":
            $ analytical_score += 2
            play sound success volume 0.33
            a "Хэш MD5: 7a3b9c... загружаю в VirusTotal."
            "Через несколько секунд появляется отчёт."
            show virustotal at screen_center with dissolve
            a "29 из 66 антивирусов детектируют угрозу. Пакет: `com.dtbekgyrd.ramroe`."
            m "Судя по меткам, троян. Интересно, еще какие-то пометки про доступ к телефонии и банкам. Хорошо, что проверил."
            hide virustotal with dissolve
            
        "Сразу удалить — это вредонос":
            $ impulsive_score += 2
            play sound bad volume 0.33

            a "Это явно вредонос. Удаляю."
            m "Стой! Ты удалил единственный источник доказательств."
            m "Теперь мы не узнаем, кто за этим стоит и какие были настройки."
            "Максим качает головой."
            m "Хорошо, что телефон еще при нас. Сейчас снова выгрузим этот файл."
            
        "Спросить Дмитрия, откуда он это скачал":
            $ practical_score += 1
            
            a "Дмитрий, откуда вы скачали этот файл?"
            d "От продавца на Авито... он сказал, без этого не увидишь фото запчастей..."
            a "Классическая социальная инженерия. Использовали ваш интерес к сделке."
    
    ## СЦЕНА 2.3: УЛИКА №2 — КОНФИГ ТРОЯНА
    
    a "Иду глубже. Папка данных приложения..."
    show configxml at screen_center with dissolve
    
    "`/data/data/com.dtbekgyrd.ramroe/prefs.xml`"
    a "Интересно, в содержимом прямо прописан параметр \"Перехвата\", и заданы номера телефонов..."
    
    # # Флешбэк №2
    # play music flashback_music fadein 1.0
    # show flashback_stress with flash
    # flashback "Банковское приложение. Дмитрий переводит 7000 рублей другу."
    # flashback "Сообщение от друга: «Верни, срочно нужны на лекарства»."
    # flashback "Дмитрий возвращает деньги. Его баланс: 1243 рубля."
    # flashback "Уведомление на Авито: «Обмен с доплатой?»"
    # hide flashback_stress with dissolve
    # stop music fadeout 1.0
    # play music tense_music fadein 1.0
    stop music
    hide configxml with dissolve
    m "Ну что, сложилось какое-нибудь представление?"
    
    menu:
        "Это SMS-троян. Он перехватывает коды от банков и QIWI.":
            $ analytical_score += 2
            
            a "Это SMS-троян. Судя по конфигу, он перехватывает коды от банков и QIWI."
            m "Верно. Видишь эти номера? Это не цели, а источники — откуда ждать SMS."
            
        "Надо срочно заблокировать номера из списка!":
            $ impulsive_score += 1
            
            a "Надо срочно заблокировать эти номера!"
            m "Подожди. Это не номера атакующих."
            m "Посмотри внимательнее — это служебные номера, например коды от банков."
            m "Блокировать их нет смысла. Это не очистит телефон от вредоносных приложений."

            
        "Нужно сперва составить отчёт по шаблону, потом уже проанализируем.":
            $ practical_score += 2
            
            a "Нужно составить отчёт по шаблону DFIR: как произошла компроментация, какое время было затрачено, какие рекомендации. Все-таки это инцидент."
            m "Правильно. Документация — половина работы."
    
    ## СЦЕНА 2.4: УЛИКА №3 — ПСИХОЛОГИЧЕСКИЙ ПОРТРЕТ
    
    show smslog at screen_center with dissolve

    m "Анализ таких данных, как SMS или фотографии, может много сказать о личности. И дать понятие, почему такое могло произойти. Все-таки нам нужно выявить факторы риска."
    
    "Анализ логов SMS. Последние недели:"
    play sound sms
    "• «Кредит просрочен 3 дня»"
    play sound sms
    "• «Мама, пришли 5000 до зарплаты»"
    play sound sms
    "• «Что, и не пил вчера совсем даже один-то??? Исправился уже?»"
    
    # # Флешбэк №3
    # play music flashback_music fadein 1.0
    # show flashback_phone with flash
    # flashback "23:47. Дмитрий засыпает на диване."
    # flashback "Телофон вибрирует. На экране: «Разрешить приложению доступ к SMS и звонкам?»"
    # flashback "Полусонный палец тянется к кнопке «Разрешить»..."
    # hide flashback_phone with dissolve
    # stop music fadeout 1.0
    # play music tense_music fadein 1.0
    
    hide smslog with dissolve

    a "Заметил подозрительную ссылку по MMS. Осмелюсь предположить - это и был вредоносный файл!"

    m "Допустим, что так. Но время не ждет - как мы можем резюмировать поведение нашего горе-продавца?"
    
    menu:
        "Объяснить причины: финансовый стресс + активность на Авито → идеальная мишень для социнженерии":
            $ analytical_score += 2
            
            a "Финансовый стресс, нужда в деньгах, активность на Авито..."
            a "Он оказался идеальной мишенью для социальной инженерии."
            m "Именно. Ты понял главное — не уязвимость в телефоне, а уязвимость в голове."
            
        "Нарушил кибергигиену — сам виноват":
            $ impulsive_score += 1
            
            a "Он сам нарушил кибергигиену. Сам виноват."
            m "Алексей..."
            m "Ты не защитник, ты судья. В SOC так не работают."
            m "Наша задача — предотвратить, а не обвинять."
            
        "Рекомендовать обучение по безопасному поведению":
            $ practical_score += 2
            
            a "Нужно рекомендовать обучение по безопасному поведению."
            a "И двухфакторную аутентификацию через приложение, а не SMS."
            m "Практичный подход. Защита на будущее."
    
    ## СЦЕНА 2.5: СБОР ЦЕПОЧКИ КОМПРОМЕТАЦИИ
    
    m "Теперь собери всю цепочку. Что было сначала, что потом."
    m "Поймёшь последовательность — поймёшь атаку."
    play music ch2fon volume 0.33
    # Вызов мини-игры
    call screen chain_minigame
    
    if chain_complete:
        play sound success volume 0.33
        $ analytical_score += 3
        m "Отлично! Ты правильно выстроил цепочку."
        m "Финансовый стресс → поиск быстрых денег → фишинг → вредонос → кража."
    else:
        $ impulsive_score += 1
        play sound bad volume 0.33
        m "Ты спутал причину и следствие."
        m "Вернись и перечитай логи. Что было мотивом, а что инструментом?"
        
    ## СЦЕНА 2.6: ЭМОЦИОНАЛЬНЫЙ ФИНАЛ ГЛАВЫ
    
    scene bg office_soc
    with fade
    
    show max_1 at Transform(ypos=0.04, zoom=1.15) with dissolve
    show alex_nb at Transform(xalign=0.95,ypos=0.1, zoom=1.03) with dissolve
    show dmitriy at Transform(center, xpos = 0.35, ypos=0.04, zoom=0.68) with dissolve
    
    m "Ты не просто нашёл вирус."
    m "Ты понял, {b}почему{/b} он сработал."
    m "Это и есть суть DFIR — цифровая криминалистика и реагирование на инциденты."
    
    d "Я думал, это просто приложение..."
    d "Я же не хакер..."
    
    hide dmitriy with moveoutright
    
    "Дмитрий уходит, всё ещё выглядит подавленным."
    
    show alex_nb at Transform(xpos = 0.35,ypos=0.1, zoom=1.03) with move
    
    a "(Получается, его не взломали...)"
    a "(Его {b}подтолкнули{/b} к ошибке.)"
    a "(И я бы, наверное, тоже… если бы искал запчасти в 2 ночи.)"
    
    m "Завтра будет новый день. И новый случай."
    m "Но сегодня ты сделал хорошую работу."
    
    stop music fadeout 3.0
    
    scene black with fade
    
    scene black with fade
    
    # Автосохранение
    $ renpy.save("chapter2_complete", "Глава 2 завершена")
        
    # Переход к главе 3
    # jump chapter_3
    
    return